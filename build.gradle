buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:latest.release'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.1'
    }
}

plugins {
    id 'pmd'
    id 'groovy'
    id 'jacoco'
    id 'findbugs'
    id 'codenarc'
    id 'checkstyle'
    id 'maven-publish'
    id 'nebula.optional-base' version '2.2.0'
    id 'pl.allegro.tech.build.axion-release' version '1.2.3'
    id 'com.bmuschko.nexus' version '2.3.1'
}

apply plugin: 'errorprone'
apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag { prefix = project.name }
    versionCreator 'versionWithBranch'
}

project.group = 'pl.allegro.tech.boot'
project.version = scmVersion.version

allprojects {
    sourceCompatibility = 1.7

    ext {
        handlebarsVersion = '2.0.0'
        springBootVersion = '1.2.3.RELEASE'
    }

    repositories {
        mavenCentral()
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:' + springBootVersion
    compile 'com.github.jknack:handlebars-springmvc:' + handlebarsVersion
    compile 'com.github.jknack:handlebars-guava-cache:' + handlebarsVersion
    compile 'com.github.jknack:handlebars-helpers:' + handlebarsVersion, optional
    compile 'com.github.jknack:handlebars-humanize:' + handlebarsVersion, optional
    compile 'com.github.jknack:handlebars-markdown:' + handlebarsVersion, optional
    compile 'com.github.jknack:handlebars-jackson2:' + handlebarsVersion, optional
    compile 'joda-time:joda-time:2.7', optional

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.springframework.boot:spring-boot-starter-test:' + springBootVersion
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

checkstyle {
    toolVersion = '6.4.1'
}

codenarc {
    toolVersion = '0.23'
    configFile = new File("${projectDir}/config/codenarc/test-rules.groovy")
}

nexusStaging {
    packageGroup = "pl.allegro"
}

modifyPom {
    project {
        name 'Spring Boot Starter Handlebars'
        description 'Spring Boot auto-configuration for Handlebars template engine'
        url 'https://github.com/allegro/spring-boot-starter-handlebars'
        inceptionYear '2015'

        scm {
            url 'https://github.com/allegro/spring-boot-starter-handlebars'
            connection 'scm:git@github.com:allegro/spring-boot-starter-handlebars.git'
            developerConnection 'scm:git@github.com:allegro/spring-boot-starter-handlebars.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'plesiecki'
                name 'Pawe≈Ç Lesiecki'
            }
        }
    }
}